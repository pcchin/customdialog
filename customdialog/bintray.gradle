/*
 * Copyright 2020 PC Chin.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.jfrog.bintray'
version = '1.0.3'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    // Add other references to the javadoc
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.getJavaCompileProvider().get().classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load( new FileInputStream("local.properties"))

// Bintray
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'customdialog'
        name = 'com.pcchin.customdialog'
        configurations = ['archives']
        desc = 'A series of custom DialogFragments for ease of use.'
        websiteUrl = 'https://github.com/pcchin/customdialog'
        issueTrackerUrl = 'https://github.com/pcchin/customdialog/issues'
        vcsUrl = 'https://github.com/pcchin/customdialog.git'
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = false
        githubRepo = 'pcchin/customdialog'

        version {
            name = '1.0.3'
            vcsTag = '1.0.3'
            gpg {
                sign = true
            }
        }
    }
}